# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    Geographic{Datum}

Geographic CRS with a given `Datum`.
"""
abstract type Geographic{Datum} <: CRS{Datum} end

ndims(::Type{<:Geographic}) = 3

"""
    GeodeticLatLon(lat, lon)
    GeodeticLatLon{Datum}(lat, lon)

Geodetic latitude `lat âˆˆ [-90Â°,90Â°]` and longitude `lon âˆˆ [-180Â°,180Â°]` in angular units (default to degree)
with a given `Datum` (default to `WGS84Latest`).

## Examples

```julia
GeodeticLatLon(45, 45) # add default units
GeodeticLatLon(45Â°, 45Â°) # integers are converted converted to floats
GeodeticLatLon((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
GeodeticLatLon(45.0Â°, 45.0Â°)
GeodeticLatLon{WGS84Latest}(45.0Â°, 45.0Â°)
```

See [EPSG:4326](https://epsg.io/4326).
"""
struct GeodeticLatLon{Datum,D<:Deg} <: Geographic{Datum}
  lat::D
  lon::D
  function GeodeticLatLon{Datum, D}(lat, lon) where {Datum,D<:Deg} 
    new{Datum,D}(checklat(lat), fixlon(lon))
  end
end

GeodeticLatLon{Datum}(lat::D, lon::D) where {Datum,D<:Deg} = GeodeticLatLon{Datum,float(D)}(lat, lon)
GeodeticLatLon{Datum}(lat::Deg, lon::Deg) where {Datum} = GeodeticLatLon{Datum}(promote(lat, lon)...)
GeodeticLatLon{Datum}(lat::Rad, lon::Rad) where {Datum} = GeodeticLatLon{Datum}(rad2deg(lat), rad2deg(lon))
GeodeticLatLon{Datum}(lat::Number, lon::Number) where {Datum} = GeodeticLatLon{Datum}(addunit(lat, Â°), addunit(lon, Â°))

GeodeticLatLon(args...) = GeodeticLatLon{WGS84Latest}(args...)

Base.convert(::Type{GeodeticLatLon{Datum,D}}, coords::GeodeticLatLon{Datum}) where {Datum,D} =
  GeodeticLatLon{Datum,D}(coords.lat, coords.lon)

raw(coords::GeodeticLatLon) = ustrip(coords.lon), ustrip(coords.lat) # reverse order

constructor(::Type{<:GeodeticLatLon{Datum}}) where {Datum} = GeodeticLatLon{Datum}

function reconstruct(C::Type{<:GeodeticLatLon}, raw)
  lon, lat = raw .* units(C)
  constructor(C)(lat, lon) # reverse order
end

lentype(::Type{<:GeodeticLatLon{Datum,D}}) where {Datum,D} = Met{numtype(D)}

==(coordsâ‚::GeodeticLatLon{Datum}, coordsâ‚‚::GeodeticLatLon{Datum}) where {Datum} =
  coordsâ‚.lat == coordsâ‚‚.lat && (coordsâ‚.lon == coordsâ‚‚.lon || (islon180(coordsâ‚.lon) && coordsâ‚.lon == -coordsâ‚‚.lon))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeodeticLatLon{Datum}}) where {Datum} =
  GeodeticLatLon{Datum}(-90 + 180 * rand(rng), -180 + 360 * rand(rng))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeodeticLatLon}) = rand(rng, GeodeticLatLon{WGS84Latest})

"""
    LatLon(lat, lon)
    LatLon{Datum}(lat, lon)

Alias to [`GeodeticLatLon`](@ref).

## Examples

```julia
LatLon(45, 45) # add default units
LatLon(45Â°, 45Â°) # integers are converted converted to floats
LatLon((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
LatLon(45.0Â°, 45.0Â°)
LatLon{WGS84Latest}(45.0Â°, 45.0Â°)
```

See [EPSG:4326](https://epsg.io/4326).
"""
const LatLon = GeodeticLatLon

"""
    GeodeticLatLonAlt(lat, lon, alt)
    GeodeticLatLonAlt{Datum}(lat, lon, alt)

Geodetic latitude `lat âˆˆ [-90Â°,90Â°]` and longitude `lon âˆˆ [-180Â°,180Â°]` in angular units (default to degree)
and altitude in length units (default to meter) with a given `Datum` (default to `WGS84Latest`).

## Examples

```julia
GeodeticLatLonAlt(45, 45, 1) # add default units
GeodeticLatLonAlt(45Â°, 45Â°, 1m) # integers are converted converted to floats
GeodeticLatLonAlt((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
GeodeticLatLonAlt(45.0Â°, 45.0Â°, 1.0km) # length quantities are converted to meters
GeodeticLatLonAlt(45.0Â°, 45.0Â°, 1.0m)
GeodeticLatLonAlt{WGS84Latest}(45.0Â°, 45.0Â°, 1.0m)
```
"""
struct GeodeticLatLonAlt{Datum,D<:Deg,M<:Met} <: Geographic{Datum}
  lat::D
  lon::D
  alt::M
  function GeodeticLatLonAlt{Datum,D,M}(lat, lon, alt) where {Datum,D<:Deg,M<:Met}
    new(checklat(lat), fixlon(lon), alt)
  end
end

GeodeticLatLonAlt{Datum}(lat::D, lon::D, alt::M) where {Datum,D<:Deg,M<:Met} =
  GeodeticLatLonAlt{Datum,float(D),float(M)}(lat, lon, alt)
GeodeticLatLonAlt{Datum}(lat::Deg, lon::Deg, alt::Met) where {Datum} =
  GeodeticLatLonAlt{Datum}(promote(lat, lon)..., alt)
GeodeticLatLonAlt{Datum}(lat::Deg, lon::Deg, alt::Len) where {Datum} =
  GeodeticLatLonAlt{Datum}(lat, lon, uconvert(m, alt))
GeodeticLatLonAlt{Datum}(lat::Rad, lon::Rad, alt::Len) where {Datum} =
  GeodeticLatLonAlt{Datum}(rad2deg(lat), rad2deg(lon), alt)
GeodeticLatLonAlt{Datum}(lat::Number, lon::Number, alt::Number) where {Datum} =
  GeodeticLatLonAlt{Datum}(addunit(lat, Â°), addunit(lon, Â°), addunit(alt, m))

GeodeticLatLonAlt(args...) = GeodeticLatLonAlt{WGS84Latest}(args...)

Base.convert(::Type{GeodeticLatLonAlt{Datum,D,M}}, coords::GeodeticLatLonAlt{Datum}) where {Datum,D,M} =
  GeodeticLatLonAlt{Datum,D,M}(coords.lat, coords.lon, coords.alt)

raw(coords::GeodeticLatLonAlt) = ustrip(coords.lon), ustrip(coords.lat), ustrip(coords.alt) # reverse order

constructor(::Type{<:GeodeticLatLonAlt{Datum}}) where {Datum} = GeodeticLatLonAlt{Datum}

function reconstruct(C::Type{<:GeodeticLatLonAlt}, raw)
  lon, lat, alt = raw .* units(C)
  constructor(C)(lat, lon, alt) # reverse order
end

lentype(::Type{<:GeodeticLatLonAlt{Datum,D,M}}) where {Datum,D,M} = M

==(coordsâ‚::GeodeticLatLonAlt{Datum}, coordsâ‚‚::GeodeticLatLonAlt{Datum}) where {Datum} =
  coordsâ‚.lat == coordsâ‚‚.lat &&
  (coordsâ‚.lon == coordsâ‚‚.lon || (islon180(coordsâ‚.lon) && coordsâ‚.lon == -coordsâ‚‚.lon)) &&
  coordsâ‚.alt == coordsâ‚‚.alt

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeodeticLatLonAlt{Datum}}) where {Datum} =
  GeodeticLatLonAlt{Datum}(-90 + 180 * rand(rng), -180 + 360 * rand(rng), rand(rng))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeodeticLatLonAlt}) =
  rand(rng, GeodeticLatLonAlt{WGS84Latest})

"""
    LatLonAlt(lat, lon, alt)
    LatLonAlt{Datum}(lat, lon, alt)

Alias to [`GeodeticLatLonAlt`](@ref).

## Examples

```julia
LatLonAlt(45, 45, 1) # add default units
LatLonAlt(45Â°, 45Â°, 1m) # integers are converted converted to floats
LatLonAlt((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
LatLonAlt(45.0Â°, 45.0Â°, 1.0km) # length quantities are converted to meters
LatLonAlt(45.0Â°, 45.0Â°, 1.0m)
LatLonAlt{WGS84Latest}(45.0Â°, 45.0Â°, 1.0m)
```
"""
const LatLonAlt = GeodeticLatLonAlt

"""
    GeocentricLatLon(lat, lon)
    GeocentricLatLon{Datum}(lat, lon)

Geocentric latitude `lat âˆˆ [-90Â°,90Â°]` and longitude `lon âˆˆ [-180Â°,180Â°]` in angular units (default to degree)
with a given `Datum` (default to `WGS84`).

## Examples

```julia
GeocentricLatLon(45, 45) # add default units
GeocentricLatLon(45Â°, 45Â°) # integers are converted converted to floats
GeocentricLatLon((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
GeocentricLatLon(45.0Â°, 45.0Â°)
GeocentricLatLon{WGS84Latest}(45.0Â°, 45.0Â°)
```
"""
struct GeocentricLatLon{Datum,D<:Deg} <: Geographic{Datum}
  lat::D
  lon::D
  function GeocentricLatLon{Datum, D}(lat, lon) where {Datum,D<:Deg}
    new(checklat(lat), fixlon(lon))
  end
end

GeocentricLatLon{Datum}(lat::D, lon::D) where {Datum,D<:Deg} = GeocentricLatLon{Datum,float(D)}(lat, lon)
GeocentricLatLon{Datum}(lat::Deg, lon::Deg) where {Datum} = GeocentricLatLon{Datum}(promote(lat, lon)...)
GeocentricLatLon{Datum}(lat::Rad, lon::Rad) where {Datum} = GeocentricLatLon{Datum}(rad2deg(lat), rad2deg(lon))
GeocentricLatLon{Datum}(lat::Number, lon::Number) where {Datum} =
  GeocentricLatLon{Datum}(addunit(lat, Â°), addunit(lon, Â°))

GeocentricLatLon(args...) = GeocentricLatLon{WGS84Latest}(args...)

Base.convert(::Type{GeocentricLatLon{Datum,D}}, coords::GeocentricLatLon{Datum}) where {Datum,D} =
  GeocentricLatLon{Datum,D}(coords.lat, coords.lon)

raw(coords::GeocentricLatLon) = ustrip(coords.lon), ustrip(coords.lat) # reverse order

constructor(::Type{<:GeocentricLatLon{Datum}}) where {Datum} = GeocentricLatLon{Datum}

function reconstruct(C::Type{<:GeocentricLatLon}, raw)
  lon, lat = raw .* units(C)
  constructor(C)(lat, lon) # reverse order
end

lentype(::Type{<:GeocentricLatLon{Datum,D}}) where {Datum,D} = Met{numtype(D)}

==(coordsâ‚::GeocentricLatLon{Datum}, coordsâ‚‚::GeocentricLatLon{Datum}) where {Datum} =
  coordsâ‚.lat == coordsâ‚‚.lat && (coordsâ‚.lon == coordsâ‚‚.lon || (islon180(coordsâ‚.lon) && coordsâ‚.lon == -coordsâ‚‚.lon))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeocentricLatLon{Datum}}) where {Datum} =
  GeocentricLatLon{Datum}(-90 + 180 * rand(rng), -180 + 360 * rand(rng))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{GeocentricLatLon}) = rand(rng, GeocentricLatLon{WGS84Latest})

"""
    AuthalicLatLon(lat, lon)
    AuthalicLatLon{Datum}(lat, lon)

Authalic latitude `lat âˆˆ [-90Â°,90Â°]` and longitude `lon âˆˆ [-180Â°,180Â°]` in angular units (default to degree)
with a given `Datum` (default to `WGS84`).

## Examples

```julia
AuthalicLatLon(45, 45) # add default units
AuthalicLatLon(45Â°, 45Â°) # integers are converted converted to floats
AuthalicLatLon((Ï€/4)rad, (Ï€/4)rad) # radians are converted to degrees
AuthalicLatLon(45.0Â°, 45.0Â°)
AuthalicLatLon{WGS84Latest}(45.0Â°, 45.0Â°)
```
"""
struct AuthalicLatLon{Datum,D<:Deg} <: Geographic{Datum}
  lat::D
  lon::D
  function AuthalicLatLon{Datum,D}(lat, lon) where {Datum,D<:Deg} 
    new(checklat(lat), fixlon(lon))
  end
end

AuthalicLatLon{Datum}(lat::D, lon::D) where {Datum,D<:Deg} = AuthalicLatLon{Datum,float(D)}(lat, fixlon(lon))
AuthalicLatLon{Datum}(lat::Deg, lon::Deg) where {Datum} = AuthalicLatLon{Datum}(promote(lat, lon)...)
AuthalicLatLon{Datum}(lat::Rad, lon::Rad) where {Datum} = AuthalicLatLon{Datum}(rad2deg(lat), rad2deg(lon))
AuthalicLatLon{Datum}(lat::Number, lon::Number) where {Datum} = AuthalicLatLon{Datum}(addunit(lat, Â°), addunit(lon, Â°))

AuthalicLatLon(args...) = AuthalicLatLon{WGS84Latest}(args...)

Base.convert(::Type{AuthalicLatLon{Datum,D}}, coords::AuthalicLatLon{Datum}) where {Datum,D} =
  AuthalicLatLon{Datum,D}(coords.lat, coords.lon)

raw(coords::AuthalicLatLon) = ustrip(coords.lon), ustrip(coords.lat) # reverse order

constructor(::Type{<:AuthalicLatLon{Datum}}) where {Datum} = AuthalicLatLon{Datum}

function reconstruct(C::Type{<:AuthalicLatLon}, raw)
  lon, lat = raw .* units(C)
  constructor(C)(lat, lon) # reverse order
end

lentype(::Type{<:AuthalicLatLon{Datum,D}}) where {Datum,D} = Met{numtype(D)}

==(coordsâ‚::AuthalicLatLon{Datum}, coordsâ‚‚::AuthalicLatLon{Datum}) where {Datum} =
  coordsâ‚.lat == coordsâ‚‚.lat && (coordsâ‚.lon == coordsâ‚‚.lon || (islon180(coordsâ‚.lon) && coordsâ‚.lon == -coordsâ‚‚.lon))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{AuthalicLatLon{Datum}}) where {Datum} =
  AuthalicLatLon{Datum}(-90 + 180 * rand(rng), -180 + 360 * rand(rng))

Random.rand(rng::Random.AbstractRNG, ::Random.SamplerType{AuthalicLatLon}) = rand(rng, AuthalicLatLon{WGS84Latest})

# ------------
# CONVERSIONS
# ------------

# Adapted from PROJ coordinate transformation software
# Initial PROJ 4.3 public domain code was put as Frank Warmerdam as copyright
# holder, but he didn't mean to imply he did the work. Essentially all work was
# done by Gerald Evenden.

# reference code: https://github.com/OSGeo/PROJ/blob/master/src/4D_api.cpp#L774

function Base.convert(::Type{GeocentricLatLon{Datum}}, coords::LatLon{Datum}) where {Datum}
  Ï• = ustrip(deg2rad(coords.lat))
  eÂ² = oftype(Ï•, eccentricityÂ²(ellipsoid(Datum)))
  Ï•â€² = atan((1 - eÂ²) * tan(Ï•))
  GeocentricLatLon{Datum}(rad2deg(Ï•â€²) * Â°, coords.lon)
end

function Base.convert(::Type{LatLon{Datum}}, coords::GeocentricLatLon{Datum}) where {Datum}
  Ï•â€² = ustrip(deg2rad(coords.lat))
  eÂ² = oftype(Ï•â€², eccentricityÂ²(ellipsoid(Datum)))
  Ï• = atan(1 / (1 - eÂ²) * tan(Ï•â€²))
  LatLon{Datum}(rad2deg(Ï•) * Â°, coords.lon)
end

# reference code: https://github.com/OSGeo/PROJ/blob/master/src/projections/healpix.cpp#L230
# reference formula: https://mathworld.wolfram.com/AuthalicLatitude.html

function Base.convert(::Type{AuthalicLatLon{Datum}}, coords::LatLon{Datum}) where {Datum}
  ğŸŒ = ellipsoid(Datum)
  Ï• = ustrip(deg2rad(coords.lat))
  e = oftype(Ï•, eccentricity(ğŸŒ))
  eÂ² = oftype(Ï•, eccentricityÂ²(ğŸŒ))

  omeÂ² = 1 - eÂ²
  sinÏ• = sin(Ï•)
  esinÏ• = e * sinÏ•
  q = omeÂ² * (sinÏ• / (1 - esinÏ•^2) - (1 / 2e) * log((1 - esinÏ•) / (1 + esinÏ•)))
  # same formula as q, but Ï• = 90Â°
  qâ‚š = omeÂ² * (1 / omeÂ² - (1 / 2e) * log((1 - e) / (1 + e)))
  qqâ‚šâ»Â¹ = q / qâ‚š

  if abs(qqâ‚šâ»Â¹) > 1
    # rounding error
    qqâ‚šâ»Â¹ = sign(qqâ‚šâ»Â¹)
  end

  Î² = asin(qqâ‚šâ»Â¹)
  AuthalicLatLon{Datum}(rad2deg(Î²) * Â°, coords.lon)
end

# reference code: https://github.com/OSGeo/PROJ/blob/master/src/auth.cpp
# reference formula: https://mathworld.wolfram.com/AuthalicLatitude.html

const _Pâ‚â‚ = 0.33333333333333333333 # 1 / 3
const _Pâ‚â‚‚ = 0.17222222222222222222 # 31 / 180
const _Pâ‚â‚ƒ = 0.10257936507936507937 # 517 / 5040
const _Pâ‚‚â‚ = 0.06388888888888888888 # 23 / 360
const _Pâ‚‚â‚‚ = 0.06640211640211640212 # 251 / 3780
const _Pâ‚ƒâ‚ = 0.01677689594356261023 # 761 / 45360

# convert authalic latitude Î² to geodetic latitude Ï•
function auth2geod(Î², eÂ²)
  eâ´ = eÂ²^2
  eâ¶ = eÂ²^3
  Pâ‚â‚ = oftype(Î², _Pâ‚â‚)
  Pâ‚â‚‚ = oftype(Î², _Pâ‚â‚‚)
  Pâ‚â‚ƒ = oftype(Î², _Pâ‚â‚ƒ)
  Pâ‚‚â‚ = oftype(Î², _Pâ‚‚â‚)
  Pâ‚‚â‚‚ = oftype(Î², _Pâ‚‚â‚‚)
  Pâ‚ƒâ‚ = oftype(Î², _Pâ‚ƒâ‚)
  Î² + (Pâ‚â‚ * eÂ² + Pâ‚â‚‚ * eâ´ + Pâ‚â‚ƒ * eâ¶) * sin(2Î²) + (Pâ‚‚â‚ * eâ´ + Pâ‚‚â‚‚ * eâ¶) * sin(4Î²) + (Pâ‚ƒâ‚ * eâ¶) * sin(6Î²)
end

function Base.convert(::Type{LatLon{Datum}}, coords::AuthalicLatLon{Datum}) where {Datum}
  Î² = ustrip(deg2rad(coords.lat))
  eÂ² = oftype(Î², eccentricityÂ²(ellipsoid(Datum)))
  Ï• = auth2geod(Î², eÂ²)
  LatLon{Datum}(rad2deg(Ï•) * Â°, coords.lon)
end

# reference code: https://github.com/OSGeo/PROJ/blob/master/src/conversions/cart.cpp
# reference formulas:
# Wikipedia - Geographic coordinate conversion (https://en.wikipedia.org/wiki/Geographic_coordinate_conversion)
# Bowring, B.R, (1976). Transformation from Spatial to Geographical Coordinates (https://doi.org/10.1179/sre.1976.23.181.323)

Base.convert(::Type{LatLon{Datum}}, coords::LatLonAlt{Datum}) where {Datum} = LatLon{Datum}(coords.lat, coords.lon)

function Base.convert(::Type{LatLonAlt{Datum}}, coords::LatLon{Datum}) where {Datum}
  T = numtype(coords.lon)
  LatLonAlt{Datum}(coords.lat, coords.lon, zero(T) * m)
end

function Base.convert(::Type{Cartesian{Datum}}, coords::LatLon{Datum}) where {Datum}
  lla = convert(LatLonAlt{Datum}, coords)
  convert(Cartesian{Datum}, lla)
end

function Base.convert(::Type{LatLon{Datum}}, coords::Cartesian{Datum,3}) where {Datum}
  lla = convert(LatLonAlt{Datum}, coords)
  convert(LatLon{Datum}, lla)
end

function Base.convert(::Type{Cartesian{Datum}}, coords::LatLonAlt{Datum}) where {Datum}
  T = numtype(coords.lon)
  ğŸŒ = ellipsoid(Datum)
  Î» = ustrip(deg2rad(coords.lon))
  Ï• = ustrip(deg2rad(coords.lat))
  h = ustrip(coords.alt)
  a = T(ustrip(majoraxis(ğŸŒ)))
  eÂ² = T(eccentricityÂ²(ğŸŒ))

  sinÏ• = sin(Ï•)
  cosÏ• = cos(Ï•)
  N = a / sqrt(1 - eÂ² * sinÏ•^2)
  Nph = N + h

  x = Nph * cosÏ• * cos(Î»)
  y = Nph * cosÏ• * sin(Î»)
  z = (N * (1 - eÂ²) + h) * sinÏ•

  Cartesian{Datum}(x * m, y * m, z * m)
end

function Base.convert(::Type{LatLonAlt{Datum}}, coords::Cartesian{Datum,3}) where {Datum}
  T = numtype(coords.x)
  ğŸŒ = ellipsoid(Datum)
  x = ustrip(uconvert(m, coords.x))
  y = ustrip(uconvert(m, coords.y))
  z = ustrip(uconvert(m, coords.z))
  a = T(ustrip(majoraxis(ğŸŒ)))
  b = T(ustrip(minoraxis(ğŸŒ)))
  eÂ² = T(eccentricityÂ²(ğŸŒ))
  eâ€²Â² = eÂ² / (1 - eÂ²)

  p = hypot(x, y)
  Ïˆ = atan(a * z, b * p)

  Î» = atan(y, x)
  Ï• = atan(z + b * eâ€²Â² * sin(Ïˆ)^3, p - a * eÂ² * cos(Ïˆ)^3)
  N = a / sqrt(1 - eÂ² * sin(Ï•)^2)
  h = p / cos(Ï•) - N

  LatLonAlt{Datum}(rad2deg(Ï•) * Â°, rad2deg(Î») * Â°, h * m)
end

# datum conversion
function Base.convert(::Type{LatLon{Datumâ‚œ}}, coords::LatLon{Datumâ‚›}) where {Datumâ‚œ,Datumâ‚›}
  cartâ‚› = convert(Cartesian{Datumâ‚›}, coords)
  cartâ‚œ = convert(Cartesian{Datumâ‚œ}, cartâ‚›)
  convert(LatLon{Datumâ‚œ}, cartâ‚œ)
end

# ----------
# FALLBACKS
# ----------

Base.convert(::Type{LatLon}, coords::CRS{Datum}) where {Datum} = convert(LatLon{Datum}, coords)

Base.convert(::Type{LatLonAlt}, coords::CRS{Datum}) where {Datum} = convert(LatLonAlt{Datum}, coords)

Base.convert(::Type{GeocentricLatLon}, coords::CRS{Datum}) where {Datum} = convert(GeocentricLatLon{Datum}, coords)

Base.convert(::Type{AuthalicLatLon}, coords::CRS{Datum}) where {Datum} = convert(AuthalicLatLon{Datum}, coords)
