# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    Mercator(x, y)
    Mercator{Datum}(x, y)

Mercator coordinates in length units (default to meter)
with a given `Datum` (default to `WGS84`).

## Examples

```julia
Mercator(1, 1) # add default units
Mercator(1m, 1m) # integers are converted converted to floats
Mercator(1.0km, 1.0km) # length quantities are converted to meters
Mercator(1.0m, 1.0m)
Mercator{WGS84Latest}(1.0m, 1.0m)
```

See [EPSG:3395](https://epsg.io/3395).
"""
struct Mercator{Datum,Shift,M<:Met} <: Projected{Datum,Shift}
  x::M
  y::M
end

Mercator{Datum,Shift}(x::M, y::M) where {Datum,Shift,M<:Met} = Mercator{Datum,Shift,float(M)}(x, y)
Mercator{Datum,Shift}(x::Met, y::Met) where {Datum,Shift} = Mercator{Datum,Shift}(promote(x, y)...)
Mercator{Datum,Shift}(x::Len, y::Len) where {Datum,Shift} = Mercator{Datum,Shift}(uconvert(m, x), uconvert(m, y))
Mercator{Datum,Shift}(x::Number, y::Number) where {Datum,Shift} = Mercator{Datum,Shift}(addunit(x, m), addunit(y, m))

Mercator{Datum}(args...) where {Datum} = Mercator{Datum,Shift()}(args...)

Mercator(args...) = Mercator{WGS84Latest}(args...)

Base.convert(::Type{Mercator{Datum,Shift,M}}, coords::Mercator{Datum,Shift}) where {Datum,Shift,M} =
  Mercator{Datum,Shift,M}(coords.x, coords.y)

constructor(::Type{<:Mercator{Datum,Shift}}) where {Datum,Shift} = Mercator{Datum,Shift}

constructor(::Type{<:Mercator{Datum}}) where {Datum} = Mercator{Datum}

constructor(::Type{<:Mercator}) = Mercator

lentype(::Type{<:Mercator{Datum,Shift,M}}) where {Datum,Shift,M} = M

==(coordsâ‚::Mercator{Datum,Shift}, coordsâ‚‚::Mercator{Datum,Shift}) where {Datum,Shift} =
  coordsâ‚.x == coordsâ‚‚.x && coordsâ‚.y == coordsâ‚‚.y

isconformal(::Type{<:Mercator}) = true

# ------------
# CONVERSIONS
# ------------

function inbounds(::Type{<:Mercator}, Î», Ï•)
  T = typeof(Î»)
  -T(Ï€) â‰¤ Î» â‰¤ T(Ï€) && -T(Ï€) / 2 < Ï• < T(Ï€) / 2
end

function formulas(::Type{<:Mercator{Datum}}, ::Type{T}) where {Datum,T}
  e = T(eccentricity(ellipsoid(Datum)))

  fx(Î», Ï•) = Î»

  fy(Î», Ï•) = asinh(tan(Ï•)) - e * atanh(e * sin(Ï•))

  fx, fy
end

function backward(::Type{<:Mercator{Datum}}, x, y) where {Datum}
  ðŸŒŽ = ellipsoid(Datum)
  e = oftype(x, eccentricity(ðŸŒŽ))
  eÂ² = oftype(x, eccentricityÂ²(ðŸŒŽ))
  omeÂ² = 1 - eÂ²

  # Ï„â€²(Ï„)
  function f(Ï„)
    sqrt1Ï„Â² = sqrt(1 + Ï„^2)
    Ïƒ = sinh(e * atanh(e * Ï„ / sqrt1Ï„Â²))
    Ï„ * sqrt(1 + Ïƒ^2) - Ïƒ * sqrt1Ï„Â²
  end

  # dÏ„â€²/dÏ„
  df(Ï„) = (omeÂ² * sqrt(1 + f(Ï„)^2) * sqrt(1 + Ï„^2)) / (1 + omeÂ² * Ï„^2)

  Ïˆ = y
  Ï„â€² = sinh(Ïˆ)
  Ï„â‚€ = abs(Ï„â€²) > 70 ? (Ï„â€² * exp(e * atanh(e))) : (Ï„â€² / omeÂ²)
  Ï„ = newton(Ï„ -> f(Ï„) - Ï„â€², df, Ï„â‚€, maxiter=5)

  Î» = x
  Ï• = atan(Ï„)

  Î», Ï•
end

# ----------
# FALLBACKS
# ----------

indomain(::Type{Mercator}, coords::CRS{Datum}) where {Datum} = indomain(Mercator{Datum}, coords)

Base.convert(::Type{Mercator}, coords::CRS{Datum}) where {Datum} = convert(Mercator{Datum}, coords)
